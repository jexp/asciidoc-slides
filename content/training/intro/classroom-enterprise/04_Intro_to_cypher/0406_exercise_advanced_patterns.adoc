[canvas-image="./img/coding-sw.jpg"]
== It's your turn!

[role="canvas-caption", position="center"]
It's your turn!

[role="img-ref"]
derivative of https://www.flickr.com/photos/adikos/4440682278["Female Typing"]
 by https://www.flickr.com/photos/adikos/[Adikos] / 
http://creativecommons.org/licenses/by/2.0/[CC BY]

== Advanced queries 

- Who is working in "Project X" at "GraphIT"?
- Who is the lead of the "QA" department at "GraphIT"?
- How many hours per week is Daniel working?
- Find the three persons with the lowest hours per week.
- Who is having the same role in projects? 
- Is Alice a superior of Daniel? 

== Project Members

.Who is working in "Project X" at "GraphIT"?
[source,cypher,options="step"]
----
MATCH (h:Human)<-[:MEMBER]-(p:Project),
  p-[:BELONGS_TO]->(:Dept)-[:PARENT*0..]->(o:Org)
WHERE p.name = "Project X" AND o.name = "GraphIT"
RETURN h.name;
----

== Department Lead 

.Who is the lead of the "QA" department at "GraphIT"?
[source,cypher,options="step"]
----
MATCH (h:Human)<-[:LEAD]-(d:Dept),
  d-[:PARENT*0..]->(o:Org)
WHERE d.name = "QA" AND o.name = "GraphIT"
RETURN h.name;
----

== Weekly Worktime

.How many hours per week is Daniel working?
[source,cypher,options="step"]
----
MATCH (h:Human)<-[e:EMPLOYEE]-(d:Dept)
WHERE h.name = "Daniel"
RETURN sum(e.hoursPerWeek) as `hours per week`;
----

== Least working people

.Find the three persons with the lowest hours per week.
[source,cypher,options="step"]
----
MATCH (h:Human)<-[e:EMPLOYEE]-(d:Dept)
RETURN h.name AS name, sum(e.hoursPerWeek) as `hours per week`
ORDER BY `hours per week`
LIMIT 3;
----

== Same Project Roles

.Who is having the same role in projects? 
[source,cypher,options="step"]
----
MATCH (h:Human)<-[m1:MEMBER]-(p1:Project),
      (h)<-[m2:MEMBER]-(p2:Project)
WHERE m1.role = m2.role
RETURN h.name AS name, p1.name, p2.name, m1.role as role;
----

== Superiors

.Is Alice a superior of Daniel?
[source,cypher,options="step"]
----
MATCH (a:Human)<-[:LEAD]-(d1:Dept),
  d1<-[:PARENT*0..]-(:Dept)-[:EMPLOYEE]->(d:Human)
WHERE a.name = "Alice" 
  AND d.name = "Daniel"
RETURN a, d;
----
