[canvas-image="./img/coding-sw.jpg"]
== It's your turn!

[role="canvas-caption", position="center"]
It's your turn!

[role="img-ref"]
derivative of https://www.flickr.com/photos/adikos/4440682278["Female Typing"]
 by https://www.flickr.com/photos/adikos/[Adikos] / 
http://creativecommons.org/licenses/by/2.0/[CC BY]

== Advanced queries 

- Who is working in "Project X" at "GraphIT"?
- Who is the lead of the "QA" department at "GraphIT"?
- How many hours per week is Daniel working?
- Find the three persons with the lowest hours per week.
- Who is having the same role in projects? 
- Is Alice a superior of Daniel? 

== Project Members

.Who is working in "Project X" at "GraphIT"?
[source,cypher,options="step"]
----
MATCH (p:Person)<-[:MEMBER]-(pr:Project),
      (pr)-[:BELONGS_TO]->(:Dept)-[:PARENT*0..]->(o:Org)
WHERE pr.name = "Project X" AND o.name = "GraphIT"
RETURN p.name;
----

== Department Lead 

.Who is the lead of the "QA" department at "GraphIT"?
[source,cypher,options="step"]
----
MATCH (p:Person)<-[:LEAD]-(d:Dept),
      (d)-[:PARENT*0..]->(o:Org)
WHERE d.name = "QA" AND o.name = "GraphIT"
RETURN p.name;
----

== Weekly Worktime

.How many hours per week is Daniel working?
[source,cypher,options="step"]
----
MATCH (p:Person)<-[e:EMPLOYEE]-(d:Dept)
WHERE p.name = "Daniel"
RETURN sum(e.hoursPerWeek) as `hours per week`;
----

== Least working people

.Find the three persons with the lowest hours per week.
[source,cypher,options="step"]
----
MATCH (p:Person)<-[e:EMPLOYEE]-(d:Dept)
RETURN p.name AS name, sum(e.hoursPerWeek) as `hours per week`
ORDER BY `hours per week`
LIMIT 3;
----

== Same Project Roles

.Who is having the same role in projects? 
[source,cypher,options="step"]
----
MATCH (p:Person)<-[m1:MEMBER]-(pr1:Project),
      (p)<-[m2:MEMBER]-(pr2:Project)
WHERE m1.role = m2.role
RETURN p.name AS name, pr1.name, pr2.name, m1.role as role;
----

== Superiors

.Is Alice a superior of Daniel?
[source,cypher,options="step"]
----
MATCH (a:Person)<-[:LEAD]-(d1:Dept),
      (d1)<-[:PARENT*0..]-(:Dept)-[:EMPLOYEE]->(d:Person)
WHERE a.name = "Alice" 
  AND d.name = "Daniel"
RETURN a, d;
----
